---
icon: pen-to-square
date: 2024-12-29
category:
  - 培训
  - ESP32
tag:
  - I2C
  - ESP32
  - OLED
  - mpu6050
order: 2  # 这是侧边栏的顺序
sticky: true # 此页面会在文章列表置顶
star: true # 此页面会出现在星标文章中
# footer: 这是测试显示的页脚 # 你可以自定义页脚
copyright: 无版权 # 你可以自定义版权信息
---

#  常见I2C设备 的使用

I2C的使用还是比较简单的，Arduino固件自带的Wire库足以让我们实现通信。但鉴于常见的模块别人已经开发了好几十年了，直接调用驱动库。

## 0.96寸OLED屏幕 的使用（内置SSD1306驱动芯片）

OLED，即有机发光二极管（Organic Light Emitting Diode）。OLED 由于同时具备自发光，不需背光源、对比度高、厚度薄、视角广、反应速度快、可用于挠曲性面板、使用温度范围广、构造及制程较简单等优异之特性，被称为是第三代显示技术。

​		LCD 都需要背光，而 OLED 不需要，因为它是自发光的。这样同样的显示 OLED 效果要来得好一些。以目前的技术，OLED 的尺寸还难以大型化，但是分辨率确可以做到很高。

​		我们今天用到的屏幕是 0.96 寸的 SSD1306 芯片驱动的 OLED 屏幕。ta的分辨率是 128*64，意思就是横向有 128 个像素点，纵向有 64 个。

> **基于Adafruit OLED库**
>
> 首先是接线：
>
> **OLED屏幕----------ESP32S3**
>
> SDA               --------->>>            GPIO3
>
> SCL                --------->>>            GPIO46

```cpp
#include <Arduino.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <Wire.h>
#define SDA1 3
#define SCl1 46
// 创建oled对象，其中第一个参数128是屏幕的宽度，第二个参数64是屏幕高度，第三个参数是I2C地址
// 第四个参数是屏幕的RST引脚，我们的屏幕没有RST引脚，给-1即可
Adafruit_SSD1306 oled(128, 64, &Wire1, -1); 

void setup()
{
    // 注意，在刚刚的创建对象时我们指定了硬件I2C_1(在arduino固件中称为Wire1),所以接下来
    // 要初始化Wire1
    Wire1.begin(SDA1, SCL1);
    // 接下来是初始化屏幕的必要操作
    oled.begin(SSD1306_SWITCHCAPVCC, 0x3C);// 设置电压3.3v和屏幕的地址0x3c
    oled.setTextColor(SSD1306_WHITE);//设置字体颜色,不管你的屏幕什么颜色,直接给白色就好
    //必要操作已完成,接下来是个性化操作
    oled.setTextSize(1);//设置字体大小，1是最小，2已经有点大了，可以自己去看看实际效果
   	到此，屏幕初始化完成了，可以选择在其它地方进行显示了，例如想在初始化屏幕之后就显示一个文本	
    oled.clearDisplay();// 先清屏，防止单片机复位后之前现实的内容和接下来要显示的文本重叠
    oled.setCursor(0, 0);// 设置光标位置
    oled.print("HELLO");
    oled.setCursor(0, 10);
    oled.print("ESP32S3");
    oled.display();// 最后调用这个方法将屏幕的内容显示出来，不可或缺
    delay(2000);// 延时2秒看看现象
}
void loop()
{
    //此处展示如何打印变量
    for (int i = 0; i <= 123; i++)
    {
        oled.clearDisplay();
    	oled.setCursor(0, 0);
    	oled.printf("current i: %d", i);
        oled.display();
        delay(10);//此处延时是为了更好地观察现象，一般来说不需要在刷新屏幕中使用延时
    }
    for (float i = 0.0f; i <= 1.23; i+=0.01)
    {
        oled.clearDisplay();
    	oled.setCursor(0, 0);
    	oled.print("current i: "+String(i));
        oled.display();
        delay(10);//此处延时是为了更好地观察现象，一般来说不需要在刷新屏幕中使用延时
    }
}
```

> 基于U8g2库(感兴趣可以自主探素)

```cpp
#include <Arduino.h>
#include <>

void setup(){}
void loop(){}
```



## MPU6050六轴姿态传感 的使用（调用DMP和不调用DMP）

MPU6050是**六轴**陀螺仪，没有地磁校准, 本身**不适合测量偏航角**，**不建议花费大量精力**浪费在对它的优化上，不如**买个九轴陀螺仪**

​													**图示为生活中常用的Yaw，Pitch，Roll**

<img src="https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E5%8F%B3%E6%89%8B%E7%AC%9B%E5%8D%A1%E5%B0%94.png?raw=true" alt="右手笛卡尔.png" style="zoom: 80%;" />

<img src="https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E7%AC%9B%E5%8D%A1%E5%B0%94math.png?raw=true" alt="笛卡尔math.png" style="zoom:80%;" />



接下来还有更直观的例子：

**1.航向yaw：**

![航向角.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E8%88%AA%E5%90%91%E8%A7%92.gif?raw=true)

**2.俯仰pitch：**

![俯仰.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E4%BF%AF%E4%BB%B0.gif?raw=true)

**3.横滚roll：**

![横滚.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E6%A8%AA%E6%BB%9A.gif?raw=true)

```cpp
/*
	使用electroniccats/MPU6050@^1.4.1库
	这是一个我修改过的使用mpu6050自带的DM获得解算后的三轴姿态，通过屏幕更新三个角度的代码
	怎么说呢，对于你们来说稍稍有些复杂，说实话，mpu6050还不如不用dmp，反正动起来都会飘
	后面会讲一个简单的库
*/
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include "I2Cdev.h"
#include "MPU6050_6Axis_MotionApps20.h"
Adafruit_SSD1306 oled(128, 64, &Wire1, -1);
/* MPU6050默认的从机地址是0x68*/
MPU6050 mpu;

#define OUTPUT_READABLE_YAWPITCHROLL
// #define OUTPUT_READABLE_QUATERNION
// #define OUTPUT_READABLE_EULER0
// #define OUTPUT_READABLE_REALACCEL
// #define OUTPUT_READABLE_WORLDACCEL
// #define OUTPUT_TEAPOT

int const INTERRUPT_PIN = 2; // Define the interruption #0 pin
bool blinkState;

/*---MPU6050 Control/Status Variables---*/
bool DMPReady = false;  // Set true if DMP init was successful
uint8_t MPUIntStatus;   // Holds actual interrupt status byte from MPU
uint8_t devStatus;      // Return status after each device operation (0 = success, !0 = error)
uint16_t packetSize;    // Expected DMP packet size (default is 42 bytes)
uint8_t FIFOBuffer[64]; // FIFO storage buffer

/*---Orientation/Motion Variables---*/
Quaternion q;        // [w, x, y, z]         Quaternion container
VectorInt16 aa;      // [x, y, z]            Accel sensor measurements
VectorInt16 gy;      // [x, y, z]            Gyro sensor measurements
VectorInt16 aaReal;  // [x, y, z]            Gravity-free accel sensor measurements
VectorInt16 aaWorld; // [x, y, z]            World-frame accel sensor measurements
VectorFloat gravity; // [x, y, z]            Gravity vector
float euler[3];      // [psi, theta, phi]    Euler angle container
float ypr[3];        // [yaw, pitch, roll]   Yaw/Pitch/Roll container and gravity vector

/*-Packet structure for InvenSense teapot demo-*/
uint8_t teapotPacket[14] = {'$', 0x02, 0, 0, 0, 0, 0, 0, 0, 0, 0x00, 0x00, '\r', '\n'};

/*------Interrupt detection routine------*/
volatile bool MPUInterrupt = false; // Indicates whether MPU6050 interrupt pin has gone high
void DMPDataReady()
{
    MPUInterrupt = true;
}

void setup()
{
    Wire.begin(SDA, SCL);
    Wire.setClock(400000); // 400kHz I2C clock. Comment on this line if having compilation difficulties
    Wire1.begin(3, 46);
    oled.begin(SSD1306_SWITCHCAPVCC, 0x3C);
    oled.setTextColor(WHITE);
    oled.setTextSize(1);
    oled.clearDisplay();
    oled.setTextWrap(true);
    /*Initialize device*/
    oled.println(F("Initializing I2C devices..."));
    oled.display();
    mpu.initialize();
    pinMode(INTERRUPT_PIN, INPUT);

    /*Verify connection*/
    oled.println(F("Testing MPU6050 connection..."));
    oled.display();
    if (mpu.testConnection() == false)
    {
        oled.println("MPU6050 connection failed");
        oled.display();
        while (true)
            ;
    }
    else
    {
        oled.println("MPU6050 connection successful");
        oled.display();
    }
    /* Initializate and configure the DMP*/
    oled.println(F("Initializing DMP..."));
    oled.display();
    devStatus = mpu.dmpInitialize();

    /* Supply your gyro offsets here, scaled for min sensitivity */
    mpu.setXGyroOffset(0);
    mpu.setYGyroOffset(0);
    mpu.setZGyroOffset(0);
    mpu.setXAccelOffset(0);
    mpu.setYAccelOffset(0);
    mpu.setZAccelOffset(0);

    /* Making sure it worked (returns 0 if so) */
    if (devStatus == 0)
    {
        mpu.CalibrateAccel(6); // Calibration Time: generate offsets and calibrate our MPU6050
        mpu.CalibrateGyro(6);
        oled.println("These are the Active offsets: ");
        mpu.PrintActiveOffsets();
        oled.println(F("Enabling DMP...")); // Turning ON DMP
        mpu.setDMPEnabled(true);
        oled.display();
        /*Enable Arduino interrupt detection*/
        oled.print(F("Enabling interrupt detection (Arduino external interrupt "));
        oled.print(digitalPinToInterrupt(INTERRUPT_PIN));
        oled.println(F(")..."));
        oled.display();
        attachInterrupt(digitalPinToInterrupt(INTERRUPT_PIN), DMPDataReady, RISING);
        MPUIntStatus = mpu.getIntStatus();

        /* Set the DMP Ready flag so the main loop() function knows it is okay to use it */
        oled.println(F("DMP ready! Waiting for first interrupt..."));
        oled.display();
        DMPReady = true;
        packetSize = mpu.dmpGetFIFOPacketSize(); // Get expected DMP packet size for later comparison
    }
    else
    {
        oled.print(F("DMP Initialization failed (code ")); // Print the error code
        oled.print(devStatus);
        oled.println(F(")"));
        oled.display();
        // 1 = initial memory load failed
        // 2 = DMP configuration updates failed
    }
    pinMode(LED_BUILTIN, OUTPUT);
    oled.setTextWrap(false);
    delay(2000);
}

void loop()
{
    if (!DMPReady)
        return; // Stop the program if DMP programming fails.

    /* Read a packet from FIFO */
    if (mpu.dmpGetCurrentFIFOPacket(FIFOBuffer))
    { // Get the Latest packet
#ifdef OUTPUT_READABLE_YAWPITCHROLL
        /* Display Euler angles in degrees */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        mpu.dmpGetGravity(&gravity, &q);
        mpu.dmpGetYawPitchRoll(ypr, &q, &gravity);
        oled.clearDisplay();
        oled.setCursor(0, 0);
        oled.printf("Yaw: %.2f\n", ypr[0] * 180 / M_PI);
        oled.setCursor(0, 10);
        oled.printf("Pitch: %.2f\n", ypr[1] * 180 / M_PI);
        oled.setCursor(0, 20);
        oled.printf("Roll: %.2f\n", ypr[2] * 180 / M_PI);
        oled.display();
#endif

#ifdef OUTPUT_READABLE_QUATERNION
        /* Display Quaternion values in easy matrix form: [w, x, y, z] */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        Serial.print("quat\t");
        Serial.print(q.w);
        Serial.print("\t");
        Serial.print(q.x);
        Serial.print("\t");
        Serial.print(q.y);
        Serial.print("\t");
        Serial.println(q.z);
#endif

#ifdef OUTPUT_READABLE_EULER
        /* Display Euler angles in degrees */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        mpu.dmpGetEuler(euler, &q);
        Serial.print("euler\t");
        Serial.print(euler[0] * 180 / M_PI);
        Serial.print("\t");
        Serial.print(euler[1] * 180 / M_PI);
        Serial.print("\t");
        Serial.println(euler[2] * 180 / M_PI);
#endif

#ifdef OUTPUT_READABLE_REALACCEL
        /* Display real acceleration, adjusted to remove gravity */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        mpu.dmpGetAccel(&aa, FIFOBuffer);
        mpu.dmpGetGravity(&gravity, &q);
        mpu.dmpGetLinearAccel(&aaReal, &aa, &gravity);
        Serial.print("areal\t");
        Serial.print(aaReal.x);
        Serial.print("\t");
        Serial.print(aaReal.y);
        Serial.print("\t");
        Serial.println(aaReal.z);
#endif

#ifdef OUTPUT_READABLE_WORLDACCEL
        /* Display initial world-frame acceleration, adjusted to remove gravity
        and rotated based on known orientation from Quaternion */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        mpu.dmpGetAccel(&aa, FIFOBuffer);
        mpu.dmpGetGravity(&gravity, &q);
        mpu.dmpGetLinearAccel(&aaReal, &aa, &gravity);
        mpu.dmpGetLinearAccelInWorld(&aaWorld, &aaReal, &q);
        Serial.print("aworld\t");
        Serial.print(aaWorld.x);
        Serial.print("\t");
        Serial.print(aaWorld.y);
        Serial.print("\t");
        Serial.println(aaWorld.z);
#endif

#ifdef OUTPUT_TEAPOT
        /* Display quaternion values in InvenSense Teapot demo format */
        teapotPacket[2] = FIFOBuffer[0];
        teapotPacket[3] = FIFOBuffer[1];
        teapotPacket[4] = FIFOBuffer[4];
        teapotPacket[5] = FIFOBuffer[5];
        teapotPacket[6] = FIFOBuffer[8];
        teapotPacket[7] = FIFOBuffer[9];
        teapotPacket[8] = FIFOBuffer[12];
        teapotPacket[9] = FIFOBuffer[13];
        Serial.write(teapotPacket, 14);
        teapotPacket[11]++; // PacketCount, loops at 0xFF on purpose
#endif

        /* Blink LED to indicate activity */
        blinkState = !blinkState;
        digitalWrite(LED_BUILTIN, blinkState);
    }
}

```



## VL53L0X激光测距芯片/模块 的使用

#### 1. 原理

采用940nm垂直腔面发射激光器（Vertical-Cavity Surface-Emitting Laser，简称VCSEL)发射出激光，激光碰到障碍物后反射回来被VL53L0X接收到，测量激光在空气中的传播时间，进而得到距离。

#### 2.参数

超小体积：4.4 x 2.4 x 1.0mm

最大测距：2m

发射的激光对眼镜安全，且完全不可见。

工作电压：2.6 to 3.5 V

通信方式：IIC，400KHz，设备地址0x52，最低位是读写标志位。0表示写，1表示读。

#### 3. 代码

评价，这是个好模块。然后就是这个模块上面的排针需要想方法焊上去，我已经在半个多月前通知过你们了
​		先把代码放上，后面我再详细介绍

```cpp
/*
	使用
		adafruit/Adafruit_VL53L0X@^1.2.4
		adafruit/Adafruit SSD1306@^2.5.13
	这两个库
*/
#include <Arduino.h>
#include "Adafruit_VL53L0X.h"
#include "Adafruit_GFX.h"
#include "Adafruit_SSD1306.h"

#define SCREEN_WIDTH 128    
#define SCREEN_HEIGHT 64    
#define OLED_RESET -1       
#define SCREEN_ADDRESS 0x3C 

Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire1, OLED_RESET);
Adafruit_VL53L0X tof = Adafruit_VL53L0X(); // 查看头文件可知默认Wire0 连接

void setup()
{   
    pinMode(20, OUTPUT);// 
    pinMode(19, OUTPUT);//这两个是当时用来根据不同距离点亮不同的灯
    Serial.begin(115200);
    Wire.begin(SDA,SCL,100000);
    Wire1.begin(SDA1, SCL1, 100000);

    tof.begin();

    oled.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS);
    oled.setTextSize(2);
    oled.clearDisplay();
    oled.setTextColor(WHITE);
    oled.setCursor(0, 0);
    oled.println("VL53L0-TOF");
    oled.setCursor(0, 25);
    oled.println("Loading...");
    oled.display();

    delay(1500);

    oled.setCursor(0, 0);
    oled.println("VL53L0-TOF");
    oled.setCursor(0, 25);
    oled.println("Loading...");
    oled.setCursor(0, 50);
    oled.println("|** OK **|");
    oled.display();
    delay(1500);
}

void loop()
{
    oled.clearDisplay();
    oled.setTextSize(2);
    

    VL53L0X_RangingMeasurementData_t measure;//该结构体是一定要有的

    tof.rangingTest(&measure, false);

    if (measure.RangeMilliMeter < 150)
    {
        digitalWrite(19, HIGH);
        digitalWrite(20, LOW);
    }
    else if (measure.RangeMilliMeter > 450)
    {
        digitalWrite(19, LOW);
        digitalWrite(20, HIGH);
    }
    else
    {
        digitalWrite(19, LOW);
        digitalWrite(20, LOW);
    }
    oled.setCursor(0, 0);
    oled.print("Distance:");
    oled.setCursor(0, 20);
    oled.printf("%d mm", measure.RangeMilliMeter);
    oled.setCursor(0, 40);
    oled.setTextSize(1);
    oled.print("Blue < 150;Red > 450");
    oled.display();

}

```
