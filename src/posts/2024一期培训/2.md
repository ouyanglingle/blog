---
icon: pen-to-square
date: 2024-12-29
category:
  - 培训
  - ESP32
tag:
  - I2C
  - ESP32
  - OLED
  - mpu6050
order: 2  # 这是侧边栏的顺序
sticky: true # 此页面会在文章列表置顶
star: true # 此页面会出现在星标文章中
# footer: 这是测试显示的页脚 # 你可以自定义页脚
copyright: 无版权 # 你可以自定义版权信息
---

#  常见I2C设备 的使用

I2C的使用还是比较简单的，Arduino固件自带的Wire库足以让我们实现通信。但鉴于常见的模块别人已经开发了好几十年了，直接调用驱动库。

## 0.96寸OLED屏幕 的使用（内置SSD1306驱动芯片）

OLED，即有机发光二极管（Organic Light Emitting Diode）。OLED 由于同时具备自发光，不需背光源、对比度高、厚度薄、视角广、反应速度快、可用于挠曲性面板、使用温度范围广、构造及制程较简单等优异之特性，被称为是第三代显示技术。

​		LCD 都需要背光，而 OLED 不需要，因为它是自发光的。这样同样的显示 OLED 效果要来得好一些。以目前的技术，OLED 的尺寸还难以大型化，但是分辨率确可以做到很高。

​		我们今天用到的屏幕是 0.96 寸的 SSD1306 芯片驱动的 OLED 屏幕。ta的分辨率是 128*64，意思就是横向有 128 个像素点，纵向有 64 个。

> **基于Adafruit OLED库**
>
> 首先是接线：
>
> **OLED屏幕----------ESP32S3**
>
> SDA               --------->>>            GPIO3
>
> SCL                --------->>>            GPIO46

```cpp
#include <Arduino.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <Wire.h>
#define SDA1 3
#define SCl1 46
// 创建oled对象，其中第一个参数128是屏幕的宽度，第二个参数64是屏幕高度，第三个参数是I2C地址
// 第四个参数是屏幕的RST引脚，我们的屏幕没有RST引脚，给-1即可
Adafruit_SSD1306 oled(128, 64, &Wire1, -1); 

void setup()
{
    // 注意，在刚刚的创建对象时我们指定了硬件I2C_1(在arduino固件中称为Wire1),所以接下来
    // 要初始化Wire1
    Wire1.begin(SDA1, SCL1);
    // 接下来是初始化屏幕的必要操作
    oled.begin(SSD1306_SWITCHCAPVCC, 0x3C);// 设置电压3.3v和屏幕的地址0x3c
    oled.setTextColor(SSD1306_WHITE);//设置字体颜色,不管你的屏幕什么颜色,直接给白色就好
    //必要操作已完成,接下来是个性化操作
    oled.setTextSize(1);//设置字体大小，1是最小，2已经有点大了，可以自己去看看实际效果
   	到此，屏幕初始化完成了，可以选择在其它地方进行显示了，例如想在初始化屏幕之后就显示一个文本	
    oled.clearDisplay();// 先清屏，防止单片机复位后之前现实的内容和接下来要显示的文本重叠
    oled.setCursor(0, 0);// 设置光标位置
    oled.print("HELLO");
    oled.setCursor(0, 10);
    oled.print("ESP32S3");
    oled.display();// 最后调用这个方法将屏幕的内容显示出来，不可或缺
    delay(2000);// 延时2秒看看现象
}
void loop()
{
    //此处展示如何打印变量
    for (int i = 0; i <= 123; i++)
    {
        oled.clearDisplay();
    	oled.setCursor(0, 0);
    	oled.printf("current i: %d", i);
        oled.display();
        delay(10);//此处延时是为了更好地观察现象，一般来说不需要在刷新屏幕中使用延时
    }
    for (float i = 0.0f; i <= 1.23; i+=0.01)
    {
        oled.clearDisplay();
    	oled.setCursor(0, 0);
    	oled.print("current i: "+String(i));
        oled.display();
        delay(10);//此处延时是为了更好地观察现象，一般来说不需要在刷新屏幕中使用延时
    }
}
```

> 基于U8g2库(感兴趣可以自主探素)

```cpp
#include <Arduino.h>
#include <>

void setup(){}
void loop(){}
```



## MPU6050六轴姿态传感 的使用（调用DMP和不调用DMP）

MPU6050是**六轴**陀螺仪，没有地磁校准, 本身**不适合测量偏航角**，**不建议花费大量精力**浪费在对它的优化上，不如**买个九轴陀螺仪**

​													**图示为生活中常用的Yaw，Pitch，Roll**

<img src="https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E5%8F%B3%E6%89%8B%E7%AC%9B%E5%8D%A1%E5%B0%94.png?raw=true" alt="右手笛卡尔.png" style="zoom: 80%;" />

<img src="https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E7%AC%9B%E5%8D%A1%E5%B0%94math.png?raw=true" alt="笛卡尔math.png" style="zoom:80%;" />



接下来还有更直观的例子：

**1.航向yaw：**

![航向角.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E8%88%AA%E5%90%91%E8%A7%92.gif?raw=true)

**2.俯仰pitch：**

![俯仰.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E4%BF%AF%E4%BB%B0.gif?raw=true)

**3.横滚roll：**

![横滚.gif](https://github.com/ouyanglingle/blog/blob/main/src/.vuepress/public/assets/images/%E6%A8%AA%E6%BB%9A.gif?raw=true)

```cpp
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include "I2Cdev.h"
#include "MPU6050_6Axis_MotionApps20.h"

// 初始化OLED显示屏，分辨率为128x64，使用Wire1进行I2C通信
Adafruit_SSD1306 oled(128, 64, &Wire1, -1);
// MPU6050默认的从机地址是0x68
MPU6050 mpu(0x68, &Wire);

int const INTERRUPT_PIN = 2; // 定义中断引脚#0
bool blinkState;

/*---MPU6050 控制/状态变量---*/
bool DMPReady = false;  // 如果DMP初始化成功则设置为true
uint8_t MPUIntStatus;   // 存储实际的MPU中断状态字节
uint8_t devStatus;      // 每次设备操作后的返回状态 (0 = 成功, !0 = 错误)
uint16_t packetSize;    // 预期的DMP数据包大小（默认为42字节）
uint8_t FIFOBuffer[64]; // FIFO存储缓冲区

/*---姿态/运动变量---*/
Quaternion q;        // [w, x, y, z] 四元数容器
VectorInt16 aa;      // [x, y, z] 加速度传感器测量值
VectorInt16 gy;      // [x, y, z] 陀螺仪传感器测量值
VectorInt16 aaReal;  // [x, y, z] 去重力加速度传感器测量值
VectorInt16 aaWorld; // [x, y, z] 世界坐标系下的加速度传感器测量值
VectorFloat gravity; // [x, y, z] 重力向量
float euler[3];      // [psi, theta, phi] 欧拉角容器
float ypr[3];        // [偏航角, 俯仰角, 滚转角] 偏航角/俯仰角/滚转角容器和重力向量

/*-InvenSense Teapot演示的数据包结构-*/
uint8_t teapotPacket[14] = {'$', 0x02, 0, 0, 0, 0, 0, 0, 0, 0, 0x00, 0x00, '\r', '\n'};

volatile bool MPUInterrupt = false; // 表示MPU6050中断引脚是否变高
void DMPDataReady()
{
    MPUInterrupt = true;
}

void setup()
{
    Serial.begin(115200);
    Wire.begin(SDA, SCL);
    Wire.setClock(400000); // 设置I2C时钟为400kHz。如果编译困难请注释掉此行
    Wire1.begin(SDA1, SCL1);
    oled.begin(SSD1306_SWITCHCAPVCC, 0x3C);
    oled.setTextColor(WHITE);
    oled.setTextSize(1);
    oled.clearDisplay();
    /* 初始化设备 */
    Serial.println(F("正在初始化I2C设备..."));
    mpu.initialize();
    pinMode(INTERRUPT_PIN, INPUT);

    /* 验证连接 */
    if (mpu.testConnection() == false)
    {
        Serial.println("MPU6050连接失败");
    }
    else
    {
        Serial.println("MPU6050连接成功");
    }
    /* 初始化并配置DMP */
    devStatus = mpu.dmpInitialize();

    /* 设置陀螺仪偏移量，按最小灵敏度缩放 */
    mpu.setXGyroOffset(0);
    mpu.setYGyroOffset(0);
    mpu.setZGyroOffset(0);
    mpu.setXAccelOffset(0);
    mpu.setYAccelOffset(0);
    mpu.setZAccelOffset(0);

    /* 确保初始化成功（返回0表示成功） */
    if (devStatus == 0)
    {
        mpu.CalibrateAccel(6); // 校准时间：生成偏移量并校准MPU6050
        mpu.CalibrateGyro(6);
        mpu.PrintActiveOffsets();
        mpu.setDMPEnabled(true);
        /* 调用Arduino外部中断 */
        attachInterrupt(digitalPinToInterrupt(INTERRUPT_PIN), DMPDataReady, RISING);
        MPUIntStatus = mpu.getIntStatus();

        /* 设置DMP就绪标志，使主循环函数知道可以使用DMP */
        DMPReady = true;
        packetSize = mpu.dmpGetFIFOPacketSize(); // 获取预期的DMP数据包大小以供后续比较
    }
    else
    {
        Serial.print(F("DMP初始化失败 (错误码 ")); // 打印错误码
        Serial.print(devStatus);
        Serial.println(F(")"));
        // 1 = 初始内存加载失败
        // 2 = DMP配置更新失败
    }
    pinMode(LED_BUILTIN, OUTPUT);
    delay(2000);
}

void loop()
{
    if (!DMPReady)
        return; // 如果DMP编程失败则停止程序

    /* 从FIFO读取一个数据包 */
    if (mpu.dmpGetCurrentFIFOPacket(FIFOBuffer))
    { // 获取最新数据包
        /* 显示欧拉角 */
        mpu.dmpGetQuaternion(&q, FIFOBuffer);
        mpu.dmpGetGravity(&gravity, &q);
        mpu.dmpGetYawPitchRoll(ypr, &q, &gravity);
        oled.clearDisplay();
        oled.setCursor(0, 0);
        oled.print("Yaw: " + String(ypr[0] * 180 / M_PI));
        oled.setCursor(0, 10);
        oled.print("Pitch: " + String(ypr[1] * 180 / M_PI));
        oled.setCursor(0, 20);
        oled.print("Roll: " + String(ypr[2] * 180 / M_PI));
        oled.display();
    }
}
```



## VL53L0X激光测距芯片/模块 的使用

#### 1. 原理

采用940nm垂直腔面发射激光器（Vertical-Cavity Surface-Emitting Laser，简称VCSEL)发射出激光，激光碰到障碍物后反射回来被VL53L0X接收到，测量激光在空气中的传播时间，进而得到距离。

#### 2.参数

超小体积：4.4 x 2.4 x 1.0mm

最大测距：2m

发射的激光对眼镜安全，且完全不可见。

工作电压：2.6 to 3.5 V

通信方式：IIC，400KHz，设备地址0x52，最低位是读写标志位。0表示写，1表示读。

#### 3. 代码

评价，这是个好模块。然后就是这个模块上面的排针需要想方法焊上去，我已经在半个多月前通知过你们了
​		先把代码放上，后面我再详细介绍

```cpp
/*
	使用
		adafruit/Adafruit_VL53L0X@^1.2.4
		adafruit/Adafruit SSD1306@^2.5.13
	这两个库
*/
#include <Arduino.h>
#include "Adafruit_VL53L0X.h"
#include "Adafruit_GFX.h"
#include "Adafruit_SSD1306.h"

#define SCREEN_WIDTH 128    
#define SCREEN_HEIGHT 64    
#define OLED_RESET -1       
#define SCREEN_ADDRESS 0x3C 

Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire1, OLED_RESET);
Adafruit_VL53L0X tof = Adafruit_VL53L0X(); // 查看头文件可知默认Wire0 连接

void setup()
{   
    pinMode(20, OUTPUT);// 
    pinMode(19, OUTPUT);//这两个是当时用来根据不同距离点亮不同的灯
    Serial.begin(115200);
    Wire.begin(SDA,SCL,100000);
    Wire1.begin(SDA1, SCL1, 100000);

    tof.begin();

    oled.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS);
    oled.setTextSize(2);
    oled.clearDisplay();
    oled.setTextColor(WHITE);
    oled.setCursor(0, 0);
    oled.println("VL53L0-TOF");
    oled.setCursor(0, 25);
    oled.println("Loading...");
    oled.display();

    delay(1500);

    oled.setCursor(0, 0);
    oled.println("VL53L0-TOF");
    oled.setCursor(0, 25);
    oled.println("Loading...");
    oled.setCursor(0, 50);
    oled.println("|** OK **|");
    oled.display();
    delay(1500);
}

void loop()
{
    oled.clearDisplay();
    oled.setTextSize(2);
    

    VL53L0X_RangingMeasurementData_t measure;//该结构体是一定要有的

    tof.rangingTest(&measure, false);

    if (measure.RangeMilliMeter < 150)
    {
        digitalWrite(19, HIGH);
        digitalWrite(20, LOW);
    }
    else if (measure.RangeMilliMeter > 450)
    {
        digitalWrite(19, LOW);
        digitalWrite(20, HIGH);
    }
    else
    {
        digitalWrite(19, LOW);
        digitalWrite(20, LOW);
    }
    oled.setCursor(0, 0);
    oled.print("Distance:");
    oled.setCursor(0, 20);
    oled.printf("%d mm", measure.RangeMilliMeter);
    oled.setCursor(0, 40);
    oled.setTextSize(1);
    oled.print("Blue < 150;Red > 450");
    oled.display();

}

```
